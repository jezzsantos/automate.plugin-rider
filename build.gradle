import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version "1.9.0"
    id 'org.jetbrains.changelog' version '1.3.1'
}

apply plugin: 'org.jetbrains.changelog'

group "${pluginGroup}"
version "${pluginVersion}"

repositories {
    mavenCentral()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://www.jetbrains.com/intellij-repository/snapshots" }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    implementation 'com.google.code.gson:gson:2.9.0'
}

test {
    useJUnitPlatform()
}

publishPlugin {
    dependsOn("patchChangelog")
    token.set(System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken"))
    channels.set(List.of(("${pluginVersion}".split("-").length > 1) ? "${pluginVersion}".split("-")[1] : "default"))
}

intellij {
    type = "${platformType}"
    version = "${platformVersion}"
    downloadSources = false
    instrumentCode = true
}

changelog {
    version = "${pluginVersion}"
    path = "${project.projectDir}/CHANGELOG.md"
    header = "[${-> version.get()}] - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+[\-\w]*)/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = ["Preamble", "Added", "Changed", "Deprecated", "Removed", "Fixed", "Security"]
}

patchPluginXml {
    changeNotes.set(changelog.get("${pluginVersion}").toHTML())
}

wrapper {
    gradleVersion = "${gradleVersion}"
}